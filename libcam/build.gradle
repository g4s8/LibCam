apply plugin: 'com.android.library'
apply from: '../q/q.gradle'
apply plugin: 'jacoco'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.g4s8'
version = new File("${rootDir}/version.txt").text.trim() + "." + System.env.TRAVIS_BUILD_NUMBER ?: "-local"

configurations {
    javadocDeps
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode System.env.TRAVIS_BUILD_NUMBER?.toInteger() ?: 1
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-annotations:25.1.1'
    javadocDeps 'com.android.support:support-annotations:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1.4'
    androidTestCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.javadocDeps
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//TODO: fill it
def siteUrl = 'https://github.com/g4s8/LibCam'
def gitUrl = 'https://github.com/g4s8/LibCam.git'

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_API_KEY

    configurations = ['archives']
    pkg {
        repo = 'maven-android'
        name = 'com.g4s8.libcam'
        licenses = ['MIT']
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        issueTrackerUrl = "${siteUrl}/issues"
        labels = ['aar', 'android']
        publicDownloadNumbers = true
    }
}

task jacocoReport(type: JacocoReport) {
    group = 'Reporting'
    description = 'Generate Jacoco coverage reports after running tests.'

    reports {
        xml {
            enabled = true
            destination "${buildDir}/reports/jacoco/jacoco.xml"
        }
        html {
            enabled = true
            destination "${buildDir}/reports/jacoco"
        }
    }

    classDirectories = fileTree(
            dir: 'build/intermediates/classes/debug',
            excludes: [
                    '**/R*.class',
                    '**/BuildConfig*',
                    '**/*Exception.class',
                    '**/*$$*'
            ]
    )

    sourceDirectories = files('src/main/java')
    executionData = files('build/jacoco/testDebugUnitTest.exec')

    doFirst {
        files('build/intermediates/classes/debug').getFiles().each { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}